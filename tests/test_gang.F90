! (C) Copyright 2022- ECMWF.
! (C) Copyright 2022- Meteo-France.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

PROGRAM TEST_GANG

USE FIELD_MODULE
USE FIELD_FACTORY_MODULE
USE FIELD_ACCESS_MODULE
USE PARKIND1
USE OMP_LIB, ONLY: OMP_GET_MAX_THREADS
IMPLICIT NONE

CLASS(FIELD_4RD), POINTER :: YLF4
TYPE(FIELD_3RD_PTR), ALLOCATABLE :: YLF3L (:)

INTEGER (KIND=JPIM), PARAMETER :: NPROMA = 32, NFLEVG = 15, NDIM = 3, NGPBLKS = 10
LOGICAL, PARAMETER :: LLPERSISTENT = .TRUE.

INTEGER (KIND=JPIM), PARAMETER :: ILB (4) = [1,0,1,1], IUB (4) = [NPROMA, NFLEVG, NDIM, NGPBLKS]

#include "abor1.intfb.h"

REAL (KIND=JPRD) :: ZCOEF (NDIM)

REAL (KIND=JPRD) :: FUNC
REAL (KIND=JPRD) :: PCOEF_
INTEGER (KIND=JPIM) :: JLON_, JLEV_, JFLD_, JBLK_
FUNC (JLON_, JLEV_, JFLD_, JBLK_) = REAL (JLON_, JPRD) + 100._JPRD * (REAL (JLEV_, JPRD) + 100._JPRD * &
                                 & (REAL (JFLD_, JPRD) + 100._JPRD * (REAL (JBLK_, JPRD))))



CALL NEW_YLF4

CALL SET_HOST_YLF4

CALL SET_DEVICE_YLF4

CALL SET_DEVICE_YLF3 ([1, 3])

CALL GET_HOST_YLF4 

CALL GET_DEVICE_YLF4

CALL GET_DEVICE_YLF3 ([2])

CALL DEL_YLF4

CONTAINS

SUBROUTINE NEW_YLF4

CALL FIELD_NEW (YLF4, CHILDREN=YLF3L, UBOUNDS=IUB, LBOUNDS=ILB, PERSISTENT=LLPERSISTENT)

CALL CHECK_STATUS

END SUBROUTINE

SUBROUTINE SET_VIEW_YLF4

INTEGER (KIND=JPIM) :: JLON, JLEV, JFLD, JBLK
REAL (KIND=JPRD), POINTER :: ZPTR3 (:,:,:)

CALL YLF4%SYNC_HOST_RDWR ()

!$OMP PARALLEL DO PRIVATE (ZPTR3, JBLK)
DO JBLK = 1, NGPBLKS
  ZPTR3 => YLF4%GET_VIEW (BLOCK_INDEX=JBLK)
  DO JFLD = 1, NDIM
   DO JLEV = 1, NFLEVG
     DO JLON = 1, NPROMA
       ZPTR3 (JLON, JLEV, JFLD) = FUNC (JLON, JLEV, JFLD, JBLK)
     ENDDO
   ENDDO
  ENDDO
ENDDO
!$OMP END PARALLEL DO

END SUBROUTINE

SUBROUTINE GET_VIEW_YLF4

INTEGER (KIND=JPIM) :: JLON, JLEV, JFLD, JBLK
REAL (KIND=JPRD), POINTER :: ZPTR3 (:,:,:)

CALL YLF4%SYNC_HOST_RDWR ()

!$OMP PARALLEL DO PRIVATE (ZPTR3, JBLK)
DO JBLK = 1, NGPBLKS
  ZPTR3 => YLF4%GET_VIEW (BLOCK_INDEX=JBLK)
  DO JFLD = 1, NDIM
   DO JLEV = 1, NFLEVG
     DO JLON = 1, NPROMA
       IF (ZPTR3 (JLON, JLEV, JFLD) /= FUNC (JLON, JLEV, JFLD, JBLK)) THEN
         PRINT *, ZPTR3 (JLON, JLEV, JFLD)
         PRINT *, FUNC (JLON, JLEV, JFLD, JBLK)
         CALL ABOR1 ('GET_VIEW_YLF4: VALUE MISMATCH')
       ENDIF
     ENDDO
   ENDDO
  ENDDO
ENDDO
!$OMP END PARALLEL DO

END SUBROUTINE

SUBROUTINE SET_VIEW_YLF3 (KFLD)

INTEGER (KIND=JPIM), INTENT (IN) :: KFLD (:)

INTEGER (KIND=JPIM) :: JLON, JLEV, JFLD, JBLK, JDIM
REAL (KIND=JPRD), POINTER :: ZPTR2 (:,:)

DO JDIM = 1, SIZE (KFLD)
  JFLD = KFLD (JDIM)
  CALL YLF3L(JFLD)%PTR%SYNC_HOST_RDWR ()
ENDDO

!$OMP PARALLEL DO PRIVATE (ZPTR2, JBLK, JFLD)
DO JBLK = 1, NGPBLKS
  DO JDIM = 1, SIZE (KFLD)
    JFLD = KFLD (JDIM)
    ZPTR2 => YLF3L(JFLD)%PTR%GET_VIEW (BLOCK_INDEX=JBLK)
    DO JLEV = 1, NFLEVG
      DO JLON = 1, NPROMA
        ZPTR2 (JLON, JLEV) = FUNC (JLON, JLEV, JFLD, JBLK)
      ENDDO
    ENDDO
  ENDDO
ENDDO
!$OMP END PARALLEL DO

END SUBROUTINE

SUBROUTINE GET_VIEW_YLF3 (KFLD)

INTEGER (KIND=JPIM), INTENT (IN) :: KFLD (:)

INTEGER (KIND=JPIM) :: JLON, JLEV, JFLD, JBLK, JDIM
REAL (KIND=JPRD), POINTER :: ZPTR2 (:,:)

DO JDIM = 1, SIZE (KFLD)
  JFLD = KFLD (JDIM)
  CALL YLF3L(JFLD)%PTR%SYNC_HOST_RDWR ()
ENDDO

!$OMP PARALLEL DO PRIVATE (ZPTR2, JBLK, JFLD)
DO JBLK = 1, NGPBLKS
  DO JDIM = 1, SIZE (KFLD)
    JFLD = KFLD (JDIM)
    ZPTR2 => YLF3L(JFLD)%PTR%GET_VIEW (BLOCK_INDEX=JBLK)
    DO JLEV = 1, NFLEVG
      DO JLON = 1, NPROMA
        IF (ZPTR2 (JLON, JLEV) /= FUNC (JLON, JLEV, JFLD, JBLK)) THEN
          PRINT *, ZPTR2 (JLON, JLEV)
          PRINT *, FUNC (JLON, JLEV, JFLD, JBLK)
          CALL ABOR1 ('GET_VIEW_YLF4: VALUE MISMATCH')
        ENDIF
      ENDDO
    ENDDO
  ENDDO
ENDDO
!$OMP END PARALLEL DO

END SUBROUTINE

SUBROUTINE SET_HOST_YLF4

REAL (KIND=JPRD), POINTER :: ZHOST4 (:,:,:,:)
INTEGER (KIND=JPIM) :: JBLK, JLEV, JLON, JFLD

ZHOST4 => GET_HOST_DATA_RDWR (YLF4)

CALL CHECK_STATUS

DO JBLK = 1, NGPBLKS
  DO JFLD = 1, NDIM
    DO JLEV = 0, NFLEVG
      DO JLON = 1, NPROMA
        ZHOST4 (JLON, JLEV, JFLD, JBLK) = FUNC (JLON, JLEV, JFLD, JBLK)
      ENDDO
    ENDDO
  ENDDO
ENDDO

CALL CHECK_STATUS

END SUBROUTINE

SUBROUTINE SET_DEVICE_YLF4

REAL (KIND=JPRD), POINTER :: ZDEVICE4 (:,:,:,:)
INTEGER (KIND=JPIM) :: JBLK, JLEV, JLON, JFLD

ZDEVICE4 => GET_DEVICE_DATA_RDWR (YLF4)

CALL CHECK_STATUS


!$acc serial present (ZDEVICE4)
DO JBLK = 1, NGPBLKS
  DO JFLD = 1, NDIM
    DO JLEV = 0, NFLEVG
      DO JLON = 1, NPROMA
        ZDEVICE4 (JLON, JLEV, JFLD, JBLK) = FUNC (JLON, JLEV, JFLD, JBLK)
      ENDDO
    ENDDO
  ENDDO
ENDDO
!$acc end serial


CALL CHECK_STATUS

END SUBROUTINE

SUBROUTINE DEL_YLF4

DEALLOCATE (YLF3L)
CALL FIELD_DELETE (YLF4)

END SUBROUTINE

SUBROUTINE GET_HOST_YLF4

REAL (KIND=JPRD), POINTER :: ZHOST4 (:,:,:,:)

INTEGER (KIND=JPIM) :: JBLK, JLEV, JLON, JFLD

IF (SIZE (YLF3L) /= NDIM) CALL ABOR1 ('SIZE MISMATCH')

ZHOST4 => GET_HOST_DATA_RDONLY (YLF4)

CALL CHECK_DIMS_YLF4 (ZHOST4)

DO JBLK = 1, NGPBLKS
  DO JFLD = 1, NDIM
    DO JLEV = 0, NFLEVG
      DO JLON = 1, NPROMA
        IF (ZHOST4 (JLON, JLEV, JFLD, JBLK) /= FUNC (JLON, JLEV, JFLD, JBLK)) THEN
          PRINT *, ZHOST4 (JLON, JLEV, JFLD, JBLK)
          PRINT *,  FUNC (JLON, JLEV, JFLD, JBLK)
          CALL ABOR1 ('GET_HOST_YLF4: VALUE MISMATCH')
        ENDIF
      ENDDO
    ENDDO
  ENDDO
ENDDO

CALL CHECK_STATUS

END SUBROUTINE

SUBROUTINE GET_DEVICE_YLF4

REAL (KIND=JPRD), POINTER :: ZDEVICE4 (:,:,:,:)

INTEGER (KIND=JPIM) :: JBLK, JLEV, JLON, JFLD

IF (SIZE (YLF3L) /= NDIM) CALL ABOR1 ('SIZE MISMATCH')

ZDEVICE4 => GET_DEVICE_DATA_RDONLY (YLF4)

CALL CHECK_DIMS_YLF4 (ZDEVICE4)

!$acc serial present (ZDEVICE4) 

DO JBLK = 1, NGPBLKS
  DO JFLD = 1, NDIM
    DO JLEV = 0, NFLEVG
      DO JLON = 1, NPROMA
        IF (ZDEVICE4 (JLON, JLEV, JFLD, JBLK) /= FUNC (JLON, JLEV, JFLD, JBLK)) THEN
          PRINT *, ZDEVICE4 (JLON, JLEV, JFLD, JBLK)
          PRINT *,  FUNC (JLON, JLEV, JFLD, JBLK)
          STOP 1
        ENDIF
      ENDDO
    ENDDO
  ENDDO
ENDDO

!$acc end serial

CALL CHECK_STATUS

END SUBROUTINE

SUBROUTINE CHECK_DIMS_YLF4 (P4)

INTEGER (KIND=JPIM) :: JDIM

REAL (KIND=JPRD), POINTER :: P4 (:,:,:,:)

DO JDIM = 1, 4
  IF (LBOUND (P4, JDIM) /= ILB (JDIM)) THEN
    PRINT *, LBOUND (P4) 
    PRINT *, ILB 
    CALL ABOR1 ('LBOUND MISMATCH')
  ENDIF
  IF (UBOUND (P4, JDIM) /= IUB (JDIM)) THEN
    PRINT *, UBOUND (P4)  
    PRINT *, IUB 
    CALL ABOR1 ('UBOUND MISMATCH')
  ENDIF
ENDDO

END SUBROUTINE

SUBROUTINE CHECK_DIMS_YLF3 (P3)

INTEGER (KIND=JPIM) :: JDIM

REAL (KIND=JPRD), POINTER :: P3 (:,:,:)

DO JDIM = 1, 2
  IF (LBOUND (P3, JDIM) /= ILB (JDIM)) CALL ABOR1 ('LBOUND MISMATCH')
  IF (UBOUND (P3, JDIM) /= IUB (JDIM)) CALL ABOR1 ('UBOUND MISMATCH')
ENDDO

IF (LBOUND (P3, 3) /= ILB (4)) CALL ABOR1 ('LBOUND MISMATCH')
IF (UBOUND (P3, 3) /= IUB (4)) CALL ABOR1 ('UBOUND MISMATCH')

END SUBROUTINE

SUBROUTINE SET_HOST_YLF3 (KFLD)

INTEGER (KIND=JPIM), INTENT (IN) :: KFLD (:)

REAL (KIND=JPRD), POINTER :: ZHOST3 (:,:,:)
INTEGER (KIND=JPIM) :: JBLK, JLEV, JLON, JFLD, JDIM

DO JDIM = 1, SIZE (KFLD)

  JFLD = KFLD (JDIM)

  IF (JFLD < 1 .OR. NDIM < JFLD) THEN
    CALL ABOR1 ('UNEXPECTED JFLD')
  ENDIF

  ZHOST3 => GET_HOST_DATA_RDWR (YLF3L (JFLD)%PTR)

  CALL CHECK_DIMS_YLF3 (ZHOST3)

  DO JBLK = 1, NGPBLKS
    DO JLEV = 0, NFLEVG
      DO JLON = 1, NPROMA
        ZHOST3 (JLON, JLEV, JBLK) = FUNC (JLON, JLEV, JFLD, JBLK)
      ENDDO
    ENDDO
  ENDDO

  CALL CHECK_STATUS

ENDDO

END SUBROUTINE

SUBROUTINE SET_DEVICE_YLF3 (KFLD)

INTEGER (KIND=JPIM), INTENT (IN) :: KFLD (:)

REAL (KIND=JPRD), POINTER :: ZDEVICE3 (:,:,:)
INTEGER (KIND=JPIM) :: JBLK, JLEV, JLON, JFLD, JDIM

DO JDIM = 1, SIZE (KFLD)

  JFLD = KFLD (JDIM)

  IF (JFLD < 1 .OR. NDIM < JFLD) THEN
    CALL ABOR1 ('UNEXPECTED JFLD')
  ENDIF

  ZDEVICE3 => GET_DEVICE_DATA_RDWR (YLF3L (JFLD)%PTR)

  CALL CHECK_DIMS_YLF3 (ZDEVICE3)

!$acc serial present (ZDEVICE3) 

  DO JBLK = 1, NGPBLKS
    DO JLEV = 0, NFLEVG
      DO JLON = 1, NPROMA
        ZDEVICE3 (JLON, JLEV, JBLK) = FUNC (JLON, JLEV, JFLD, JBLK)
      ENDDO
    ENDDO
  ENDDO

!$acc end serial


  CALL CHECK_STATUS

ENDDO

END SUBROUTINE

SUBROUTINE GET_DEVICE_YLF3 (KFLD)

INTEGER (KIND=JPIM), INTENT (IN) :: KFLD (:)

REAL (KIND=JPRD), POINTER :: ZDEVICE3 (:,:,:)
INTEGER (KIND=JPIM) :: JBLK, JLEV, JLON, JFLD, JDIM

DO JDIM = 1, SIZE (KFLD)

  JFLD = KFLD (JDIM)

  IF (JFLD < 1 .OR. NDIM < JFLD) THEN
    CALL ABOR1 ('UNEXPECTED JFLD')
  ENDIF

  ZDEVICE3 => GET_DEVICE_DATA_RDONLY (YLF3L (JFLD)%PTR)

  CALL CHECK_DIMS_YLF3 (ZDEVICE3)


!$acc serial present (ZDEVICE3) 

  DO JBLK = 1, NGPBLKS
    DO JLEV = 0, NFLEVG
      DO JLON = 1, NPROMA
        IF (ZDEVICE3 (JLON, JLEV, JBLK) /= FUNC (JLON, JLEV, JFLD, JBLK)) THEN
          PRINT *, ZDEVICE3 (JLON, JLEV, JBLK)
          PRINT *,  FUNC (JLON, JLEV, JFLD, JBLK)
          STOP 1
        ENDIF
      ENDDO
    ENDDO
  ENDDO

!$acc end serial


  CALL CHECK_STATUS

ENDDO

END SUBROUTINE

SUBROUTINE GET_HOST_YLF3 (KFLD)

INTEGER (KIND=JPIM), INTENT (IN) :: KFLD (:)

REAL (KIND=JPRD), POINTER :: ZHOST3 (:,:,:)
INTEGER (KIND=JPIM) :: JBLK, JLEV, JLON, JFLD, JDIM

DO JDIM = 1, SIZE (KFLD)

  JFLD = KFLD (JDIM)

  IF (JFLD < 1 .OR. NDIM < JFLD) THEN
    CALL ABOR1 ('UNEXPECTED JFLD')
  ENDIF

  ZHOST3 => GET_HOST_DATA_RDONLY (YLF3L (JFLD)%PTR)

  CALL CHECK_DIMS_YLF3 (ZHOST3)

  DO JBLK = 1, NGPBLKS
    DO JLEV = 0, NFLEVG
      DO JLON = 1, NPROMA
        IF (ZHOST3 (JLON, JLEV, JBLK) /= FUNC (JLON, JLEV, JFLD, JBLK)) THEN
          PRINT *, ZHOST3 (JLON, JLEV, JBLK)
          PRINT *,  FUNC (JLON, JLEV, JFLD, JBLK)
          STOP 1
        ENDIF
      ENDDO
    ENDDO
  ENDDO

  CALL CHECK_STATUS

ENDDO

END SUBROUTINE

SUBROUTINE CHECK_STATUS

INTEGER (KIND=JPIM) :: JFLD

DO JFLD = 1, NDIM
  IF (YLF4%ISTATUS /= YLF3L(JFLD)%PTR%ISTATUS) THEN
    PRINT *, JFLD
    WRITE (*, '("YLF4 = ",Z16.16)') YLF4%ISTATUS
    WRITE (*, '("YLF3L (",I0,")=",Z16.16)') JFLD, YLF3L(JFLD)%PTR%ISTATUS
    CALL ABOR1 ('STATUS MISMATCH')
  ENDIF
ENDDO

END SUBROUTINE

END PROGRAM 
